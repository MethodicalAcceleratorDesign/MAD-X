!TITLE,'Test input for PTC_TWISS';

!=========== RING PARAMETERS ===========================================
call, file = "../share/ring_lattice/ring.seq";
beam, particle = proton, energy =   eg        , 
                         sigt=      0.077     , 
                         sige=      1.1e-4    , 
                         npart=     1.05e11   , 
                         exn=4*en,  eyn=4*en  , 
                         kbunch = 10,
                         et = 0.002, bv = 1,
                         ex=epsx,   ey=epsy;

option, echo, warn, info;

! At the end of the ring the data_block is produced

SAVEBETA, label=TWSSip, place=#E,sequence=fivecell;

select,flag=twiss,column=name,s,betx,bety,dx,dy;

TWISS,file=twiss_madx_ring; 

SHOW,TWSSip;     ! SHOW parameters (see "*.out" file)


! Initialize PTC
ptc_create_universe;
ptc_create_layout,model=2,method=6,nst=10,exact;
ptc_align;

select,flag=ptc_twiss,column=name,s,angle,beta11,beta21,
                                  beta12,beta22,disp1,disp3,x,px,y,py;

ptc_twiss,closed_orbit,icase=5,file=twiss_ptc_ring,no=2;

PTC_NORMAL,closed_orbit,maptable,icase=5,no=2;

x_end  = table(map_table,c1_000000,coef);
px_end = table(map_table,c2_000000,coef);
!y_end  = table(map_table,c3_000000,coef); !zeros are not filled, so the row does not exist 
!py_end = table(map_table,c4_000000,coef); !and it would returen arbitrary small number
!t_end  = table(map_table,c6_000000,coef);
!pt_end = table(map_table,c5_000000,coef);

print, text="orbit at the end from ptc_normal:";
value,x_end,px_end;

ptc_end;

!write,table="map_table",file="map_table";
write,table="map_table",file="ring_matrix_at_end";

!=========== TRANSFER-LINE PARAMETERS ==================================== 

call, file = "../share/line_lattice/line.seq";
beam, particle = proton, energy =   eg        , 
                         sigt=      0.077     , 
                         sige=      1.1e-4    , 
                         npart=     1.05e11   , 
                         exn=4*en,  eyn=4*en  , 
                         kbunch = 10,
                         et = 0.002, bv = 1,
                         ex=epsx,   ey=epsy;
option, echo, warn, info;

ptc_create_universe;
ptc_create_layout,model=2,method=6,nst=10,exact;
ptc_align;
                                    
! Example 3: Read transfer matrix from table ============================

!ptc_setswitch, debuglevel=3;

ptc_twiss,icase=5,no=2,initial_matrix_table, file=twiss_ptc_line;

ptc_twiss,icase=5,no=2,initial_matrix_table, file=twiss_ptc_line.orbit0, ignore_map_orbit;

ptc_end;

stop;
